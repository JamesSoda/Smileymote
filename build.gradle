import com.sun.org.apache.xalan.internal.xsltc.cmdline.Compile
import org.apache.tools.ant.filters.ReplaceTokens

group 'com.gmail.zaxarner'
version '2.0.1'

buildscript {
    ext.kotlin_version = '1.0.6'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8

// Automatic YAML versions
processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: version])
    }
}

// Force UTF-8
tasks.withType(Compile) {
    options.encoding = 'UTF-8'
}

// Remove /out/ folder on clean
clean.doFirst {
    delete "$rootDir/out/"
}

// Bundles all of the compiled jars into a nice and sane directory. (/out/)
tasks.withType(Jar) {
    destinationDir = file("$rootDir/out/")
}

configurations {
    provided
    compile.extendsFrom provided
}

artifacts {
    archives jar
}

jar {
    dependsOn configurations.runtime
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "*.yml"
    }
}

repositories {
    mavenCentral()

    maven {
        name 'bukkit-repo'
        url 'http://repo.bukkit.org/content/groups/public/'
    }

    maven {
        name 'Spigot'
        url 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }

    maven {
        url "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    provided group: 'org.bukkit', name: 'bukkit', version: '1.14.4-R0.1-SNAPSHOT'
    provided group: 'org.spigotmc', name: 'spigot-api', version: '1.14.4-R0.1-SNAPSHOT'
}